        /**
         * Checks if this save slot contains any data.
         * @return True if data exists, false if slot is empty
         */
        public function hasData():Boolean {
            return this.url != null;
        }
        
        /**
         * Clears all data from this save slot.
         * @param callback Function to call when clearing is complete
         * @param thisArg Context to use when calling the callback
         */
        public function clearData(callback:Function = null, thisArg:* = null):void {
            if (this.core) this.core.callComponent("CloudSave.clearSlot", {id: this.id}, function(response:io.newgrounds.models.objects.Response, callabckParams:*=null):void {

                var error:NgioError = null;
                
                // request was good, check the result
                if (response && response.success) {
                    
                    var result:io.newgrounds.models.results.CloudSave.clearSlotResult = response.getResult() as io.newgrounds.models.results.CloudSave.clearSlotResult;
                    
                    if (!result) {
                        error = Errors.getError(Errors.INVALID_RESPONSE, "Invalid response format received from CloudSave.clearSlot", true);
                    } else if (!result.success) {
                        error = result.error;
                    }

                } else {
                    error = response.error;
                }
                
                if (callback != null) {
                    callback.call(thisArg, error);
                }
            }, this);
        }
        
        /**
         * Loads the raw string data from the URL stored on this save slot.
         * @param callback Function to call with the raw data string or null
         * @param thisArg Context to use when calling the callback
         */
        public function loadDataRaw(callback:Function = null, thisArg:* = null):void {
            
            // If this is an empty slot, skip the load and just return null data to the callback.
            if (this.url == null || this.url.length == 0) {
                if (callback != null) {
                    callback.call(thisArg, null, null);
                }
                return;
            }
			
            var self:* = this;
            var loader:URLLoader = new URLLoader();
            loader.dataFormat = URLLoaderDataFormat.TEXT;
            loader.addEventListener(Event.COMPLETE, function(e:Event):void {
                self.error = null;
                if (callback != null) {
                    callback.call(thisArg, loader.data as String, self.error);
                }
            });
            loader.addEventListener(IOErrorEvent.IO_ERROR, function(e:IOErrorEvent):void {
                if (callback != null) {
                    self.error = Errors.getError(Errors.INVALID_RESPONSE, "Failed to load SaveSlot data from URL: " + e.text, true);
                    callback.call(thisArg, null, self.error);
                }
            });

            // the original v3 API did not include the protocol in the URL, so we need to add it if it's missing to prevent load errors.
            // it's possible this will be updated on the server some day, so we should be prepared to handle both cases. If the URL already includes the protocol, this will have no effect.
            var loadUrl:String = this.url;
            if (loadUrl.indexOf("://") == -1) {
                loadUrl = "https:" + loadUrl;
            }
            loader.load(new URLRequest(loadUrl));
        }
		
        /**
         * Saves raw string data to this save slot.
         * @param data The raw string data to save
         * @param callback Function to call when saving is complete
         * @param thisArg Context to use when calling the callback
         */
        public function saveDataRaw(data:String, callback:Function = null, thisArg:* = null):void {
            if (this.core) this.core.callComponent("CloudSave.setData", {id: this.id, data: data}, function(response:io.newgrounds.models.objects.Response, callbackParams:*=null):void {
                
                var error:NgioError = null;
                
                // request was good, check the result
                if (response && response.success) {

                    var result:io.newgrounds.models.results.CloudSave.setDataResult = response.getResult() as io.newgrounds.models.results.CloudSave.setDataResult;
                    
                    if (!result.success) {
                        error = result.error;
                    }

                } else {
                    error = response.error;
                }
                
                if (callback != null) {
                    callback.call(thisArg, error);
                }
            }, this);
        }
        
        /**
         * Loads data from this save slot and parses it as JSON.
         * @param callback Function to call with the decoded object
         * @param thisArg Context to use when calling the callback
         */
        public function loadData(callback:Function = null, thisArg:* = null):void {
            var self:* = this;
            this.loadDataRaw(function(data:String, error:*):void {
                var decoded:* = null;
                if (data != null) {
                    try {
                        decoded = io.newgrounds.NGJSON.parse(data);
                    } catch (error:Error) {
                        self.error = Errors.getError(Errors.INVALID_RESPONSE, "Failed to decode SaveSlot data JSON: " + error.message, true);
                        decoded = null;
                    }
                }
                if (decoded != null) {
                    self.error = null;
                } else if (error != null) {
                    self.error = error;
                }
                if (callback != null) {
                    callback.call(thisArg, decoded, self.error);
                }
            }, this);
        }
		
        /**
         * Encodes data as JSON and saves it to this save slot.
         * @param data The object to encode and save
         * @param callback Function to call when saving is complete
         * @param thisArg Context to use when calling the callback
         */
        public function saveData(data:*, callback:Function = null, thisArg:* = null):void {
            this.saveDataRaw(io.newgrounds.NGJSON.stringify(data), callback, thisArg);
        }
		
        /**
         * Returns a string representation of this save slot.
         */
        override public function toString():String {
            return (this.id != 0) ? "SaveSlot #" + this.id : "SaveSlot #0";
        }
        
        /**
         * Clears session-specific data to return the save slot to its unauthenticated state.
         * Called by AppState when the user's session expires or is cleared.
         */
        public function clearSessionData():void {
            this.datetime = null;
            this.timestamp = 0;
            this.size = 0;
            this.url = null;
        }
